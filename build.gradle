plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven'
    id 'maven-publish'
    id 'pmd'
    id 'com.diffplug.gradle.spotless' version '3.24.1' apply false
    id 'com.github.johnrengelman.shadow' version '5.1.0' apply false
    id 'com.github.spotbugs' version '2.0.0' apply false
    id 'org.gradle.playframework' version '0.6' // TODO: Does not support Play 2.7.x
}

allprojects {
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.github.spotbugs'

    repositories {
        mavenLocal()
        jcenter()
    }

    spotless {
        java {
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
            importOrder 'java', 'javax', 'org', 'com', 'm.core'
            removeUnusedImports()
        }
    }

    spotbugs {
        ignoreFailures = true // Disabling for TravisCI
        toolVersion = '3.1.10'
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

}

sourceCompatibility = 8
targetCompatibility = 8

// Force maven local publish and code coverage after tests
check.dependsOn jacocoTestReport, publishToMavenLocal

repositories {
    // Play Framework https://gradle.github.io/playframework/#applying_the_plugin
    maven {
        name "lightbend-maven-release"
        url "https://repo.lightbend.com/lightbend/maven-releases"
    }
    ivy {
        name "lightbend-ivy-release"
        url "https://repo.lightbend.com/lightbend/ivy-releases"
        layout "ivy"
    }
}

dependencies {
    // Using `api` instead of `implementation` to work around
    // pom generation scope issue : https://github.com/gradle/gradle/issues/1118
    api 'org.slf4j:slf4j-simple:1.7.25' // logging
    api 'org.apache.commons:commons-lang3:3.9' // number and string utils
    api 'org.glassfish.jersey.core:jersey-common:2.27' // uri template
    api 'org.aeonbits.owner:owner:1.0.10' // configuration file handling
    api 'org.aeonbits.owner:owner:1.0.10' // configuration file handling
    api 'com.google.guava:guava:27.0.1-jre' // multimap api for many to many
    api 'com.google.code.gson:gson:2.8.5' // json library
    api 'javax.servlet:javax.servlet-api:4.0.1' // for cookie and session

    api 'com.mashape.unirest:unirest-java:1.4.9' // rest client
    api 'org.apache.httpcomponents:httpclient:4.5.6' // rest http cookie
    api 'org.apache.httpcomponents:httpclient-cache:4.5.6' // rest client caching

    api 'commons-dbutils:commons-dbutils:1.7' // jdbc utils
    api 'mysql:mysql-connector-java:8.0.14' // jdbc drivers
    api 'org.postgresql:postgresql:42.2.5' // jdbc drivers
    api 'org.xerial:sqlite-jdbc:3.25.2' // jdbc drivers
    api 'org.apache.derby:derby:10.14.2.0' // jdbc drivers
    api 'org.hsqldb:hsqldb:2.4.1' // jdbc drivers
    api 'org.mapdb:mapdb:3.0.7' // embedded key value database
    api 'com.h2database:h2:1.4.197' // embedded JDBC database
    api 'redis.clients:jedis:3.0.1' // client for key value store
    api 'org.redisson:redisson:3.3.0' // client for key value store
    api 'org.mongodb:mongo-java-driver:3.9.1' // client for doc store

    api 'com.sparkjava:spark-core:2.8.0' // rest framework
    api ('org.springframework.boot:spring-boot-starter-webflux:2.1.2.RELEASE') { // rest framework
        // Use app logging https://docs.spring.io/spring-boot/docs/current/reference/html/howto-logging.html#howto-configure-log4j-for-logging
        exclude module: 'spring-boot-starter-logging'
    }
    api 'org.springframework.session:spring-session-core:2.1.2.RELEASE' // rest framework session
    api 'io.vertx:vertx-web:3.6.2' // rest framework
    // Using the default version from https://gradle.github.io/playframework/#play_framework
    api 'com.typesafe.play:play-server_2.12:2.6.15' // rest framework
    api 'com.typesafe.play:play-java_2.12:2.6.15' // rest framework
    api 'com.typesafe.play:play_2.12:2.6.15' // rest framework

    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

// Maven publishing (Working around scope issue : https://github.com/gradle/gradle/issues/1118)
publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'm.core'
            artifactId = 'mcore-all'
            version = '0.1.1'
            from components.java
            pom {
                name = 'mCore'
                description = 'Microservices Framework'
                url = 'm.core.hackorama.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hackorama'
                        name = 'Kishan Thomas'
                        email = 'kishan.thomas@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/hackorama/mcore'
                }
            }
        }
    }
}

// Play Framework (https://gradle.github.io/playframework/#targeting-play-version)
play {
    platform {
        playVersion = '2.6.15'
        scalaVersion = '2.12'
        javaVersion = JavaVersion.VERSION_1_8
    }
}

task testSpring(type: Test) {
    environment 'SERVER_TYPE', 'SPRING' // Repeat all the tests with Spring server
    dependsOn cleanTest
}

task testVertx(type: Test) {
    environment 'SERVER_TYPE', 'VERTX' // Repeat all the tests with Vertx server
    dependsOn cleanTest
}

task testPlay(type: Test) {
    environment 'SERVER_TYPE', 'PLAY' // Repeat all the tests with Play server
    dependsOn cleanTest
}


jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

test {
    environment 'SERVER_TYPE', 'SPARK' // Default tests using Spark server
    maxParallelForks = 1
    finalizedBy jacocoTestReport
    dependsOn cleanTest
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

pmd {
    incrementalAnalysis = true
    ignoreFailures = true
    ruleSetFiles = files(file("config/pmd/ruleset.xml"))
    ruleSets = []
}
