plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven'
    id 'maven-publish'
    id 'pmd'
    id 'com.diffplug.gradle.spotless' version '3.24.1'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'com.github.spotbugs' version '2.0.0'
    id 'org.gradle.playframework' version '0.6' // TODO: Does not support Play 2.7.x
}

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenLocal()
    jcenter()
    // Play Framework https://gradle.github.io/playframework/#applying_the_plugin
    maven {
        name "lightbend-maven-release"
        url "https://repo.lightbend.com/lightbend/maven-releases"
    }
    ivy {
        name "lightbend-ivy-release"
        url "https://repo.lightbend.com/lightbend/ivy-releases"
        layout "ivy"
    }
}

dependencies {
    // Using `compile` instead of `implementation` to work around
    // pom generation scope issue : https://github.com/gradle/gradle/issues/1118
    compile 'org.slf4j:slf4j-simple:1.7.25' // logging
    compile 'org.apache.commons:commons-lang3:3.8.1' // number and string utils
    compile 'org.glassfish.jersey.core:jersey-common:2.27' // uri template
    compile 'org.aeonbits.owner:owner:1.0.10' // configuration file handling
    compile 'org.aeonbits.owner:owner:1.0.10' // configuration file handling
    compile 'com.google.guava:guava:27.0.1-jre' // multimap compile for many to many
    compile 'com.google.code.gson:gson:2.8.5' // json library
    compile 'javax.servlet:javax.servlet-api:4.0.1' // for cookie and session

    compile 'com.mashape.unirest:unirest-java:1.4.9' // rest client
    compile 'org.apache.httpcomponents:httpclient:4.5.6' // rest http cookie
    compile 'org.apache.httpcomponents:httpclient-cache:4.5.6' // rest client caching

    compile 'commons-dbutils:commons-dbutils:1.7' // jdbc utils
    compile 'mysql:mysql-connector-java:8.0.14' // jdbc drivers
    compile 'org.postgresql:postgresql:42.2.5' // jdbc drivers
    compile 'org.xerial:sqlite-jdbc:3.25.2' // jdbc drivers
    compile 'org.apache.derby:derby:10.14.2.0' // jdbc drivers
    compile 'org.hsqldb:hsqldb:2.4.1' // jdbc drivers
    compile 'org.mapdb:mapdb:3.0.7' // embedded key value database
    compile 'com.h2database:h2:1.4.197' // embedded JDBC database
    compile 'redis.clients:jedis:3.0.1' // client for key value store
    compile 'org.redisson:redisson:3.3.0' // client for key value store
    compile 'org.mongodb:mongo-java-driver:3.9.1' // client for doc store

    compile 'com.sparkjava:spark-core:2.8.0' // rest framework
    compile ('org.springframework.boot:spring-boot-starter-webflux:2.1.2.RELEASE') { // rest framework
        // Use app logging https://docs.spring.io/spring-boot/docs/current/reference/html/howto-logging.html#howto-configure-log4j-for-logging
        exclude module: 'spring-boot-starter-logging'
    }
    compile 'org.springframework.session:spring-session-core:2.1.2.RELEASE' // rest framework session
    compile 'io.vertx:vertx-web:3.6.2' // rest framework
    // Using the default version from https://gradle.github.io/playframework/#play_framework
    compile 'com.typesafe.play:play-server_2.12:2.6.15' // rest framework
    compile 'com.typesafe.play:play-java_2.12:2.6.15' // rest framework
    compile 'com.typesafe.play:play_2.12:2.6.15' // rest framework

    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

// Maven publishing (Working around scope issue : https://github.com/gradle/gradle/issues/1118)

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'm.core'
            artifactId = 'mcore-all'
            version = '0.1.1'
            from components.java
            pom {
                name = 'mCore'
                description = 'Microservices Framework'
                url = 'm.core.hackorama.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hackorama'
                        name = 'Kishan Thomas'
                        email = 'kishan.thomas@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/hackorama/mcore'
                }
            }
        }
    }
}

// Play Framework (https://gradle.github.io/playframework/#targeting-play-version)

play {
    platform {
        playVersion = '2.6.15'
        scalaVersion = '2.12'
        javaVersion = JavaVersion.VERSION_1_8
    }
}

// code formatting

spotless {
    java {
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
        importOrder 'java', 'javax', 'org', 'com', 'm.core'
        removeUnusedImports()
    }
}

// code coverage

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

test {
    environment 'SERVER_TYPE', 'SPARK' // Default tests using Spark server
    maxParallelForks = 1
    finalizedBy jacocoTestReport
    dependsOn cleanTest
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task testSpring(type: Test) {
    environment 'SERVER_TYPE', 'SPRING' // Repeat all the tests with Spring server
    dependsOn cleanTest
}

task testVertx(type: Test) {
    environment 'SERVER_TYPE', 'VERTX' // Repeat all the tests with Vertx server
    dependsOn cleanTest
}

task testPlay(type: Test) {
    environment 'SERVER_TYPE', 'PLAY' // Repeat all the tests with Play server
    dependsOn cleanTest
}

check.dependsOn jacocoTestReport

// code quality

spotbugs {
    ignoreFailures = true // Disabling for TravisCI
    toolVersion = '3.1.10'
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = true
    ruleSetFiles = files(file("config/pmd/ruleset.xml"))
    ruleSets = []
}
